\subsection{\texttt{gsa.sc}}
\label{gsasc}

\begin{lstlisting}[basicstyle=\footnotesize\ttfamily,language=Java]
/*
GSA <https://github.com/yannics/GSA/blob/master/gsa.pdf>
Sound design studies
version 1.0.1
<by.cmsc@gmail.com>
*/

Ulam {
	*ar { |ar, stretch=5, nx=0, ny=\max, sig=\norm, detune=0, rndAmp=1, mul=1, add=0|
		var signal=Silent.ar;
		ar.detune(detune).do{ |n|
		  signal = FSinOsc.ar(n) 
		    * EnvGen.kr(Env.collatz(n, stretch, nx, ny), doneAction:2) * rrand(rndAmp, 1) + signal;
		};
		if (sig == \norm)
		{
			signal = signal.range(-1, 1)
		};
		if (sig == \tanh)
		{
			signal = signal.tanh
		};
		signal = signal * mul + add;
		^signal
	}
}

Delbuf {
	*ar { |buf, del=0, rate=1, da=2, mul=1, add=0|
		var signal;
		signal = PlayBuf.ar(1, buf, BufRateScale.kr(buf)
		  * rate);
		signal = DelayC.ar(signal, 30, del);
		DetectSilence.ar(signal, doneAction:da);
		signal = signal * mul + add;
		^signal
	}
}

Sow {
	*ar { |buf, ser, sym=\sup, del=0, mul=1, add=0|
		var signal, maxAmpIndex, arr;
		var file = SoundFile.openRead(buf.path);
		var ar = FloatArray.newClear(file.numFrames 
		  * file.numChannels);
		file.readData(ar);
		maxAmpIndex = ar.abs.maxIndex;
		arr = ser.harmRatio(sym, maxAmpIndex,
		  file.sampleRate, del);
		signal = Mix.new(arr.collect({ |sub| 
		  Delbuf.ar(buf, sub[1], sub[0], 0)}));
		DetectSilence.ar(signal, doneAction:2);
		signal = signal * mul + add;
		^signal
	}
}

+ Array  {
	harmRatio {
		| sym=\sup, ind=1, sr=1, del=0 |
		var res, i;
		var arr=this.as(Set).as(Array).sort;
		if (sym == \sup)
		{
		  res=(arr/this.minItem).reciprocal.reverse
		};
		if (sym == \inf)
		{
		  res=(arr/this.minItem)*this.maxItem.reciprocal
		};
		res=res.collect{|it| 
		  [it, res.minItem.reciprocal-it.reciprocal
		    * (ind/sr)]};
		if (del.asBoolean)
		{
			i = 0;
			while({i < res.last[1]}, {i = del + i});
			i=i-res.last[1];
			res = res.collect{|it| [it[0], it[1]+i]};
		}
			^res
	}
	
	detune  {
		|n=3, len=1000|
		var tmp;
		tmp = this.collect{|it| it+n.rand2};
		if((len < this.size) && (len > 0))
		{
			^tmp.scramble[0..len-1]
		}
		{
			^tmp
		}
	}
}
\end{lstlisting} 